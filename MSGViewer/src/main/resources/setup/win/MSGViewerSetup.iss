; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "MSGViewer"
#define MyAppVersion "1.9"
#define MyAppPublisher "Redeye Labs"
#define MyAppURL "https://sourceforge.net/p/msgviewer"
#define MyAppExeName "MSGViewer.jar"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{72F0165E-E966-4748-A3F9-8F3765D3345A}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
OutputDir=output
OutputBaseFilename=MSGViewerSetup-{#MyAppVersion}
Compression=lzma
SolidCompression=yes
ChangesAssociations=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"
Name: "FileTypeAssoc"; Description: "{cm:AssocMsg}"; GroupDescription: "{cm:FileTypeAssocGroup}";

[Files]
Source: "..\..\dist\MSGViewer.jar"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\dist\lib\*"; DestDir: "{app}\lib"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "HSWShellExec_x86.dll"; DestDir: "{app}"; Flags: 32bit
Source: "HSWShellExec_amd64.dll"; DestDir: "{app}"; Flags: 64bit; Check: IsWin64
Source: "..\icon.ico"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\mail.ico"; DestDir: "{app}"; Tasks: FileTypeAssoc

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\icon.ico";
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\icon.ico"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Flags: nowait postinstall shellexec skipifsilent; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"

[Registry]
Root: "HKCR"; Subkey: ".msg"; ValueType: string; ValueData: "MSGViewer"; Flags: uninsdeletevalue; Tasks: FileTypeAssoc
Root: "HKCR"; Subkey: "MSGViewer"; ValueType: string; ValueData: "Outlook Message Files"; Flags: uninsdeletekey; Tasks: FileTypeAssoc
Root: "HKCR"; Subkey: "MSGViewer\DefaultIcon"; ValueType: string; ValueData: "{app}\mail.ico"; Flags: uninsdeletekey; Tasks: FileTypeAssoc
Root: "HKCR"; Subkey: "MSGViewer\shell\open\command"; ValueType: string; ValueData: """{code:getJavaHome}\bin\javaw.exe"" ""-jar"" ""{app}\{#MyAppExeName}"" ""%1"""; Flags: uninsdeletekey; Tasks: FileTypeAssoc

[CustomMessages]
english.FileTypeAssocGroup=File extensions:
german.FileTypeAssocGroup=Dateiendung:
english.AssocMsg="Associate "".msg"" extension"
german.AssocMsg=Öffne .msg Dateien automatisch mit dem MSGViewer.

[Code]
function getJavaHome(Param: String) : String;
var
  JavaVersion: String;
  JavaHome: String;
begin
  RegQueryStringValue(HKLM, 'SOFTWARE\JavaSoft\Java Runtime Environment\', 'CurrentVersion', JavaVersion);
  RegQueryStringValue(HKLM, 'SOFTWARE\JavaSoft\Java Runtime Environment\'+ JavaVersion, 'JavaHome', JavaHome);

  Result:=JavaHome;
end;

function InitializeSetup(): Boolean;
var
 ErrorCode: Integer;
 JavaInstalled : Boolean;
 Result1 : Boolean;
 Versions: TArrayOfString;
 I: Integer;
begin
 if RegGetSubkeyNames(HKLM, 'SOFTWARE\JavaSoft\Java Runtime Environment', Versions) then
 begin
  for I := 0 to GetArrayLength(Versions)-1 do
   if JavaInstalled = true then
   begin
    //do nothing
   end else
   begin
    if ( Versions[I][2]='.' ) and ( ( StrToInt(Versions[I][1]) > 1 ) or ( ( StrToInt(Versions[I][1]) = 1 ) and ( StrToInt(Versions[I][3]) >= 7 ) ) ) then
    begin
     JavaInstalled := true;
    end else
    begin
     JavaInstalled := false;
    end;
   end;
 end else
 begin
  JavaInstalled := false;
 end;


 //JavaInstalled := RegKeyExists(HKLM,'SOFTWARE\JavaSoft\Java Runtime Environment\1.9');
 if JavaInstalled then
 begin
  Result := true;
 end else
    begin
  Result1 := MsgBox('This tool requires Java Runtime Environment version 1.7 or newer to run. Please download and install the JRE and run this setup again. Do you want to download it now?',
   mbConfirmation, MB_YESNO) = idYes;
  if Result1 = false then
  begin
   Result:=false;
  end else
  begin
   Result:=false;
   ShellExec('open',
    'http://www.java.com',
    '','',SW_SHOWNORMAL,ewNoWait,ErrorCode);
  end;
    end;
end;


end.
